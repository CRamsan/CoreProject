buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$globalKotlinVersion"
    }
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.multiplatform"
apply plugin: "kotlin-kapt"

android {
    buildToolsVersion globalAndroidBuildToolsVersion
    compileSdkVersion globalCompileSdkVersion
    defaultConfig {
        minSdkVersion globalMinSdkVersion
        targetSdkVersion globalTargetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility globalJavaCompatibility
        targetCompatibility globalJavaCompatibility
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$globalOkhttpVersion"
    implementation "com.google.code.gson:gson:$globalGsonVerion"
    implementation "com.microsoft.appcenter:appcenter-analytics:$globalAppCenterSdkVersion"
    implementation "com.microsoft.appcenter:appcenter-crashes:$globalAppCenterSdkVersion"

    testImplementation "androidx.test.ext:junit-ktx:$globalAndroidXJUnitVersion"
    testImplementation "io.mockk:mockk:$globalMockkVersion"
    testImplementation "io.mockk:mockk-android:$globalMockkVersion"
    testImplementation "junit:junit:$globalJUnitVersion"
    testImplementation "org.robolectric:robolectric:$globalRobolectricVersion"
}

kotlin {
    android("android") {
        compilations.all {
            kotlinOptions {
                jvmTarget = globalJavaCompatibility
            }
        }
    }
    jvm("jvm") {
        compilations.all {
            kotlinOptions {
                jvmTarget = globalJavaCompatibility
            }
        }
    }
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")

                implementation "io.mockk:mockk-common:$globalMockkVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$globalCoroutinesVersion"
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin("stdlib-jdk8")
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation "io.mockk:mockk:$globalMockkVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$globalCoroutinesVersion"
            }
        }
        androidMain {
            dependencies {
                implementation kotlin("stdlib-jdk8")
            }
        }
        androidTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$globalCoroutinesVersion"
            }
        }
        iosMain {
            dependencies {
            }
        }
        iosTest {
            dependencies {
            }
        }
    }
}

tasks {
    jvmTest {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'test'
}
