plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.kotlin.android.extensions"
    id "io.gitlab.arturbosch.detekt"
}

android {
    compileSdkVersion globalCompileSdkVersion
    defaultConfig {
        minSdkVersion globalMinSdkVersion
        targetSdkVersion globalTargetSdkVersion
        versionCode globalVersionCode
        versionName globalVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility globalJavaCompatibility
        targetCompatibility globalJavaCompatibility
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

/**
 * Configuring detekt for static analysis
 */
detekt {
    toolVersion = "$globalDetektVersion"
    input = files("src/main/java", "src/test/java", "src/androidTest/java",
                  "src/commonMain/", "src/commonTest",
                  "src/iosMain/", "src/iosTest")
    filters = ".*_/resources/.*,.*_/build/.*"
    config = files("detekt-config.yml")
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

check.dependsOn ktlint

dependencies {
    implementation "org.kodein.di:kodein-di-erased-jvm:$globalKodeinVersion"

    testImplementation "androidx.test.ext:junit-ktx:$globalAndroidXJUnitVersion"
    testImplementation "io.mockk:mockk:$globalMockkVersion"
    testImplementation "junit:junit:$globalJUnitVersion"
    testImplementation "org.robolectric:robolectric:$globalRobolectricVersion"
}

kotlin {
    android("android") {
    }
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")

                implementation "io.mockk:mockk-common:$globalMockkVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$globalCoroutinesVersion"
                implementation "org.kodein.di:kodein-di-core:$globalKodeinVersion"
                implementation "org.kodein.di:kodein-di-erased:$globalKodeinVersion"
            }
        }
        androidMain {
            dependencies {
                implementation kotlin("stdlib-jdk8")
            }
        }
        androidTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$globalCoroutinesVersion"
            }
        }
        iosMain {
            dependencies {
            }
        }
        iosTest {
            dependencies {
            }
        }
    }
}

configurations {
    // Workaround for https://youtrack.jetbrains.com/issue/KT-27170
    compileClasspath
}
