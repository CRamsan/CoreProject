buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$globalKotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$globalAndroidXNavigationVersion"
    }
}

repositories {
    google()
    jcenter()
    maven {
        url "https://kotlin.bintray.com/kotlinx/"
    }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    buildToolsVersion globalAndroidBuildToolsVersion
    compileSdkVersion globalCompileSdkVersion
    defaultConfig {
        applicationId "com.cramsan.petproject"
        minSdkVersion globalMinSdkVersion
        targetSdkVersion globalTargetSdkVersion
        versionName "0.12"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
		stage {
            initWith release
            applicationIdSuffix ".stage"
            matchingFallbacks = ['release']
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility globalJavaCompatibility
        targetCompatibility globalJavaCompatibility
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = globalJavaCompatibility
        }
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    implementation project(":petproject:appcore")
    implementation project(":framework")

    implementation packages.ConstraintLayout
    implementation packages.AndroidxActivityKtx
    implementation packages.AndroidxFragmentKtx
    implementation packages.AndroidXLifecycleExt
    implementation packages.AndroidXLifecycleViewModelKtx
    implementation packages.AndroidXPagingRuntimeKtx
    implementation packages.AndroidXRecyclerView
    implementation packages.AndroidXPreferences
    implementation packages.AndroidXPreferencesKtx
    implementation packages.AndroidXNavigationFragmentKtx
    implementation packages.AndroidXNavigationUIKtx
    implementation packages.AndroidXWorkManagerRuntimeKtx
    implementation packages.AndroidXWorkManagerRuntime
    implementation packages.AndroidMaterial
    implementation packages.Glide
    implementation packages.KtorAndroid

    implementation packages.KotlinStdJdk8
    implementation packages.CoroutinesCore
    implementation packages.CoroutinesAndroid

    implementation packages.Kodein
    implementation packages.KodeinAndroid

    implementation packages.AppCenterSdk

    kapt packages.GlideCompiler

    testImplementation packages.JUnit
    androidTestImplementation packages.AndroidXJUnit
    androidTestImplementation packages.AndroidXJUnitKtx
    androidTestImplementation packages.AndroidXTest
    androidTestImplementation packages.AndroidXTestRules
    androidTestImplementation packages.AndroidXTestRunner
    androidTestImplementation packages.EspressoIntents
    androidTestImplementation packages.Espresso
    androidTestImplementation packages.UIAutomator
    androidTestImplementation packages.AndroidXNavigationTesting
    androidTestImplementation packages.AndroidXWorkManagerTesting
    androidTestImplementation packages.Mockito
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn ':petproject:appcore:release'
    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'test'
    dependsOn 'assembleAndroidTest'
}