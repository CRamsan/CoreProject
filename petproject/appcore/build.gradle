buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$globalKotlinVersion"
        classpath "com.squareup.sqldelight:gradle-plugin:$globalSqlDelightVersion"
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.multiplatform"
apply plugin: "kotlin-kapt"
apply plugin: "com.squareup.sqldelight"

repositories {
    mavenCentral()
}
sqldelight {
    PetProjectDB {
        packageName = "com.cramsan.petproject.db"
    }
}

android {
    buildToolsVersion globalAndroidBuildToolsVersion
    compileSdkVersion globalCompileSdkVersion
    defaultConfig {
        minSdkVersion globalMinSdkVersion
        targetSdkVersion globalTargetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility globalJavaCompatibility
        targetCompatibility globalJavaCompatibility
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    implementation "com.squareup.sqldelight:android-driver:$globalSqlDelightVersion"
    implementation "io.ktor:ktor-client-android:$globalKtorVersion"
    implementation "io.ktor:ktor-client-gson:$globalKtorVersion"

    testImplementation "androidx.test.ext:junit-ktx:$globalAndroidXJUnitVersion"
    testImplementation "com.squareup.sqldelight:android-driver:$globalSqlDelightVersion"
    testImplementation "io.mockk:mockk:$globalMockkVersion"
    testImplementation "junit:junit:$globalJUnitVersion"
    testImplementation "org.robolectric:robolectric:$globalRobolectricVersion"
}

kotlin {
    android("android") {
        compilations.all {
            kotlinOptions {
                jvmTarget = globalJavaCompatibility
            }
        }
    }

    jvm("jvm") {
        compilations.all {
            kotlinOptions {
                jvmTarget = globalJavaCompatibility
            }
        }
    }
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$globalCoroutinesVersion"
                implementation "io.ktor:ktor-client-core:$globalKtorVersion"
                implementation "io.ktor:ktor-client-json:$globalKtorVersion"
                implementation project(":framework")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
                implementation "io.mockk:mockk-common:$globalMockkVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$globalCoroutinesVersion"
                implementation "org.kodein.di:kodein-di-core:$globalKodeinVersion"
                implementation "org.kodein.di:kodein-di-erased:$globalKodeinVersion"
                implementation project(":framework")
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin("stdlib-jdk8")
                implementation "io.ktor:ktor-client-apache:$globalKtorVersion"
                implementation "io.ktor:ktor-client-gson:$globalKtorVersion"
                implementation "com.squareup.sqldelight:sqlite-driver:$globalSqlDelightVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$globalCoroutinesVersion"
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation "io.mockk:mockk:$globalMockkVersion"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$globalCoroutinesVersion"
                implementation "org.kodein.di:kodein-di-erased-jvm:$globalKodeinVersion"
            }
        }
        androidMain {
            dependencies {
                implementation kotlin("stdlib-jdk8")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$globalCoroutinesVersion"
            }
        }
        androidTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$globalCoroutinesVersion"
            }
        }
        iosMain {
            dependencies {
            }
        }
        iosTest {
            dependencies {
            }
        }
    }
}

/**
 * Log test results
 */
tasks {
    jvmTest {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

build.finalizedBy ktlint
