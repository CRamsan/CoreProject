buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$globalKotlinVersion"
        classpath "com.squareup.sqldelight:gradle-plugin:$globalSqlDelightVersion"
    }
}

repositories {
    google()
    jcenter()
    maven {
        url "https://kotlin.bintray.com/kotlinx/"
    }
}

apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.multiplatform"
apply plugin: "kotlin-kapt"
apply plugin: "com.squareup.sqldelight"

sqldelight {
    PetProjectDB {
        packageName = "com.cramsan.petproject.db"
    }
}

android {
    buildToolsVersion globalAndroidBuildToolsVersion
    compileSdkVersion globalCompileSdkVersion
    defaultConfig {
        minSdkVersion globalMinSdkVersion
        targetSdkVersion globalTargetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility globalJavaCompatibility
        targetCompatibility globalJavaCompatibility
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    implementation packages.SqlDelightAndroidDriver
    implementation packages.KtorAndroid
    implementation packages.KtorGson

    testImplementation packages.AndroidXJUnitKtx
    testImplementation packages.SqlDelightAndroidDriver
    testImplementation packages.Mockk
    testImplementation packages.JUnit
    testImplementation packages.Robolectric
}

kotlin {
    android("android") {
        compilations.all {
            kotlinOptions {
                jvmTarget = globalJavaCompatibility
            }
        }
    }

    jvm("jvm") {
        compilations.all {
            kotlinOptions {
                jvmTarget = globalJavaCompatibility
            }
        }
    }
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation project(":framework")
                implementation packages.KotlinStdCommon
                implementation packages.CoroutinesCore
                implementation packages.KtorCore
                implementation packages.KtorJson
                implementation packages.KotlinxDatetime
            }
        }
        commonTest {
            dependencies {
                implementation project(":framework")
                implementation packages.KotlinTestCommon
                implementation packages.KotlinTestAnnotations
                implementation packages.MockkCommon
                implementation packages.CoroutinesCore
                implementation packages.Kodein
            }
        }
        jvmMain {
            dependencies {
                implementation packages.KotlinStdJdk8
                implementation packages.KtorApache
                implementation packages.KtorGson
                implementation packages.SqlDelightDriver
                implementation packages.CoroutinesCore
            }
        }

        jvmTest {
            dependencies {
                implementation packages.KotlinTest
                implementation packages.KotlinTestJunit
                implementation packages.Mockk
                implementation packages.CoroutinesCore
                implementation packages.Kodein
            }
        }
        androidMain {
            dependencies {
                implementation packages.KotlinStdJdk8
                implementation packages.CoroutinesAndroid
            }
        }
        androidTest {
            dependencies {
                implementation packages.KotlinTest
                implementation packages.KotlinTestJunit
                implementation packages.CoroutinesAndroid
            }
        }
        iosMain {
            dependencies {
            }
        }
        iosTest {
            dependencies {
            }
        }
    }
}

/**
 * Log test results
 */
tasks {
    jvmTest {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn ':framework:release'
    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'allTests'
}