plugins {
    id "org.springframework.boot" version "$globalSpringFrameworkVersion"
    id "io.spring.dependency-management" version "$globalSpringDependencyManagement"
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.spring"
    id "org.jetbrains.kotlin.plugin.jpa"
    id 'war'
}

group = 'com.cramsan.petproject.webservice'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = globalJavaCompatibility
targetCompatibility = globalJavaCompatibility

repositories {
    maven {
        url "https://kotlin.bintray.com/kotlinx/"
    }
}

dependencies {
    implementation project(":petproject:appcore")
    implementation project(":framework")

    implementation packages.KotlinStdJdk8

    implementation packages.SpringDevtools
    implementation packages.SpringStarter
    implementation packages.SpringThymeleaf
    implementation packages.SpringWeb

    providedRuntime packages.SpringTomcat

    testImplementation packages.SpringTest
}

bootWar {
    providedClasspath("src/main/resources/PetProject.sql")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = globalJavaCompatibility
    }
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn ':petproject:appcore:release'
    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'test'
}