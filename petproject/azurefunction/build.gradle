plugins {
  id "com.microsoft.azure.azurefunctions" version "$globalAzureFunctionGradlePlugin"
  id "org.jetbrains.kotlin.jvm"
}

repositories {
    maven {
        url "https://kotlin.bintray.com/kotlinx/"
    }
}

group = 'com.cramsan.petproject.azurefunction'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = globalJavaCompatibility
targetCompatibility = globalJavaCompatibility

dependencies {
    implementation project(":petproject:appcore")
    implementation project(":framework")

    implementation packages.KotlinStdJdk8
    implementation packages.AzureFunctionsJavaLibrary
    implementation packages.Gson
    implementation packages.JacksonDatabinding

    testImplementation packages.Mockk
    testImplementation packages.JUnit
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = globalJavaCompatibility
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

azurefunctions {
    resourceGroup = 'PetProject'
    appName = 'petproject-api'
    pricingTier = 'Consumption'
    region = 'centralus'
    runtime {
      os = 'windows'
    }    
    localDebug = "transport=dt_socket,server=y,suspend=n,address=5005"
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn ':petproject:appcore:release'
    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'test'
    dependsOn 'azureFunctionsPackageZip'
}