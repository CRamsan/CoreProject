plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "org.jetbrains.kotlin.android.extensions"
    id "io.gitlab.arturbosch.detekt"
}

android {
    compileSdkVersion globalCompileSdkVersion
    defaultConfig {
        applicationId "com.cramsan.petproject"
        minSdkVersion globalMinSdkVersion
        targetSdkVersion globalTargetSdkVersion
        versionCode globalVersionCode
        versionName globalVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility globalJavaCompatibility
        targetCompatibility globalJavaCompatibility
    }

    packagingOptions {
        exclude 'META-INF/kotlinx-coroutines-core.kotlin_module'
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

check.dependsOn ktlint

detekt {
    toolVersion = "$globalDetektVersion"
    input = files("src/main/java",
                  "src/test/java",
                  "src/androidTest/java")
    filters = ".*_/resources/.*,.*_/build/.*"
    config = files("detekt-config.yml")
}

def constraintLayoutVersion = "1.1.3"
def androidXLifecycleVersion = "2.2.0-alpha01"
def androidXRecyclerViewVersion = "1.0.0"
def androidMaterialVersion = "1.0.0"

dependencies {
    implementation project(":appcore")
    implementation project(":framework")
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidXLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidXLifecycleVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXRecyclerViewVersion"
    implementation "com.google.android.material:material:$androidMaterialVersion"

    implementation "com.github.bumptech.glide:glide:$globalGlideVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$globalKotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$globalCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$globalCoroutinesVersion"

    implementation "org.kodein.di:kodein-di-erased-jvm:$globalKodeinVersion"
    implementation "org.kodein.di:kodein-di-framework-android-x:$globalKodeinVersion"

    kapt "com.github.bumptech.glide:compiler:$globalGlideVersion"

    testImplementation "junit:junit:$globalJUnitVersion"

    androidTestImplementation "androidx.test.ext:junit:$globalAndroidXJUnitVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:$globalAndroidXJUnitVersion"
    androidTestImplementation "androidx.test:rules:$globalAndroidXJUnitVersion"
    androidTestImplementation "androidx.test:runner:$globalAndroidXJUnitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$globalEspressoVersion"
}
