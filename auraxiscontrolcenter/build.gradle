buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidGradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$globalKotlinVersion"
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

android {
    buildToolsVersion globalAndroidBuildToolsVersion
    compileSdkVersion globalCompileSdkVersion
    defaultConfig {
        applicationId "com.cesarandres.ps2link"
        minSdkVersion globalMinSdkVersion
        targetSdkVersion globalTargetSdkVersion
        versionName "5.4.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        stage {
            initWith release
            applicationIdSuffix ".stage"
            matchingFallbacks = ['release']
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    compileOptions {
        sourceCompatibility globalJavaCompatibility
        targetCompatibility globalJavaCompatibility
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = globalJavaCompatibility
        }
    }
}

dependencies {
    implementation project(":framework")

    implementation packages.Volley
    implementation packages.Gson
    implementation packages.PrettyTime
    implementation packages.Twitter4J
    implementation packages.AndroidXAppCompat
    implementation packages.EspressoIdlingResource

    implementation packages.KotlinStdJdk8
    implementation packages.CoroutinesCore
    implementation packages.CoroutinesAndroid

    implementation packages.Kodein
    implementation packages.KodeinAndroid

    implementation packages.AppCenterSdk

    testImplementation packages.JUnit

    androidTestImplementation packages.AndroidXJUnit
    androidTestImplementation packages.AndroidXJUnitKtx
    androidTestImplementation packages.AndroidXTest
    androidTestImplementation packages.AndroidXTestRules
    androidTestImplementation packages.AndroidXTestRunner
    androidTestImplementation packages.EspressoIntents
    androidTestImplementation packages.Espresso
    androidTestImplementation packages.UIAutomator
    androidTestImplementation packages.AppCenterEspressoTestExt
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn ':framework:release'
    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'test'
    dependsOn 'assembleAndroidTest'
}