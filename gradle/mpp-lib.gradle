apply plugin: "com.android.library"
apply plugin: "org.jetbrains.kotlin.multiplatform"
apply plugin: "kotlin-kapt"

android {
    buildToolsVersion versions_tools.AndroidBuildToolsVersion
    compileSdkVersion versions_tools.CompileSdkVersion
    defaultConfig {
        versionCode libraryVersionCode
        versionName libraryVersionName
        minSdkVersion versions_tools.MinSdkVersion
        targetSdkVersion versions_tools.TargetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility versions_tools.JavaCompatibilityForAndroid
        targetCompatibility versions_tools.JavaCompatibilityForAndroid
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    implementation packages.KotlinStdJdk8
    implementation packages.CoroutinesCore
    implementation packages.CoroutinesAndroid

    testImplementation packages.AndroidXJUnitKtx
    testImplementation packages.CoroutinesTest
    testImplementation packages.JUnit
    testImplementation packages.KotlinTest
    testImplementation packages.KotlinTestJunit
    testImplementation packages.Mockk
    testImplementation packages.MockkAndroid

    testImplementation packages.Robolectric
}

kotlin {
    android("android") {
        compilations.all {
            kotlinOptions {
                jvmTarget = versions_tools.JavaCompatibilityForAndroid
            }
        }
    }
    jvm("jvm") {
        compilations.all {
            kotlinOptions {
                jvmTarget = versions_tools.JavaCompatibility
            }
        }
    }
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework()
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation packages.KotlinStdCommon
                implementation packages.CoroutinesCore
            }
        }
        commonTest {
            dependencies {
                implementation packages.KotlinTestCommon
                implementation packages.KotlinTestAnnotationsCommon
                implementation packages.MockkCommon
            }
        }
        jvmMain {
            dependencies {
                implementation packages.KotlinStdJdk8
                implementation packages.CoroutinesCore
            }
        }
        jvmTest {
            dependencies {
                implementation packages.CoroutinesTest
                implementation packages.KotlinTest
                implementation packages.KotlinTestJunit
                implementation packages.Mockk
            }
        }
        androidMain {
            dependencies {
            }
        }
        androidTest {
            dependencies {
            }
        }
        iosMain {
            dependencies {
            }
        }
        iosTest {
            dependencies {
            }
        }
    }
}

tasks {
    jvmTest {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'jvmTest'
    dependsOn 'test'
}