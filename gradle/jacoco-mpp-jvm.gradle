/**
 * Apply jacoco for the JVM target in a MPP project.
 * This plugin expects jvmTestTaskName to have the name of the task to run jvm tests.
 * Example: ext.jvmTestTaskName = "jvmTest"
 */
apply plugin: 'jacoco'

jacoco {
    toolVersion = versions_tools.Jacoco
}

project.afterEvaluate {

    task "jacocoJvmKMMTestReport" (type:JacocoReport, dependsOn: "$jvmTestTaskName") {
        group = 'verification'
        description = "Generate Jacoco JVM in KMM coverage report."

        classDirectories.setFrom(files([
                fileTree(dir: "${project.buildDir}/classes/kotlin/jvm/")
        ]))

        def coverageSourceDirs = [
                "src/main/java",
                "src/main/kotlin"
        ]

        sourceDirectories.setFrom(files(coverageSourceDirs))
        additionalSourceDirs.setFrom(files(coverageSourceDirs))
        executionData(files("${project.buildDir}/jacoco/${jvmTestTaskName}.exec"))

        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}