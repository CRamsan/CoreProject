import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.22.0"
    }
}

/**
 * Configure the detekt plugin
 */

apply plugin: "io.gitlab.arturbosch.detekt"

detekt {
    // For more information: https://detekt.github.io/detekt/gradle.html
    buildUponDefaultConfig = true
    autoCorrect = true

    def localConfigFile = file("$projectDir/config/detekt-config.yml")
    def globalConfigFile = file("$rootDir/config/detekt-config.yml")
    if (localConfigFile.exists() && globalConfigFile.exists()) {
        // Use both global and local config files
        config = files(globalConfigFile.path, localConfigFile.path)
    } else if (localConfigFile.exists()) {
        // Global config file does not exist, use only the local one.
        config = files(localConfigFile.path)
    } else if (globalConfigFile.exists()) {
        // Local config file does not exist, use only the global one.
        config = files(globalConfigFile.path)
    }

    def baselineFile = file("$projectDir/config/detekt-baseline.xml")
    if (baselineFile.exists()) {
        // a way of suppressing issues before introducing detekt
        baseline = baselineFile
    }

    dependencies {
        detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:_")
    }
}
