/**
 * Configure a Kotlin-JVM project with safe defaults.
 */

apply plugin: "org.jetbrains.kotlin.js"
apply from: "$rootDir/gradle/detekt.gradle"

kotlin {
    js(IR) {
        binaries.executable()
    }
}

compileKotlinJs{
    kotlinOptions {
        allWarningsAsErrors = true
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
}

dependencies {
    implementation meta_packages.CoreJS

    testImplementation meta_packages.CoreTestJS
}

// Release task that will build and test the project.
def releaseJs = tasks.register("releaseJs") {
    group = 'build'
    description = 'Run all the steps to build a Js artifact'

    dependsOn 'detekt'
    dependsOn 'compileKotlinJs'
    dependsOn 'test'
}

// Function to add a dependency to another JS project.
ext.registerDependency = { project ->
    def dependency = "${project}:releaseJs"
    println("Adding dependency to $dependency")
    releaseJs.configure { dependsOn(dependency) }
}

tasks.register("release") {
    group = 'build'

    dependsOn "releaseJs"
}