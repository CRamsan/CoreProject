/**
 * Configure a Kotlin-JVM project with safe defaults.
 */

apply plugin: 'kotlin'
apply plugin: "org.jetbrains.dokka"
apply plugin: 'jacoco'
apply from: "$rootDir/gradle/ktlint.gradle"
apply from: "$rootDir/gradle/detekt.gradle"

jacoco {
    toolVersion = versions_tools.Jacoco
}
jacocoTestReport {
    reports {
        xml.required = true
        html.enabled = true
    }
}

dependencies {
    implementation meta_packages.CoreJVM

    testImplementation meta_packages.CoreTestJVM
}

// Define the Kotlin jvm target
compileKotlin {
    kotlinOptions {
        jvmTarget = versions_tools.JavaCompatibility
        allWarningsAsErrors = true
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = versions_tools.JavaCompatibility
        allWarningsAsErrors = true
    }
}

// Enable outputting the results of the tests
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Release task that will build and test the project.
def releaseJvm = tasks.register("releaseJvm") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'detekt' // Run the code analyzer
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
}

// Function to add a dependency to another JVM project.
ext.registerDependency = { project ->
    def dependency = "${project}:releaseJvm"
    println("Adding dependency to $dependency")
    releaseJvm.configure { dependsOn(dependency) }
}

tasks.register("release") {
    group = 'build'

    dependsOn "releaseJvm"
}