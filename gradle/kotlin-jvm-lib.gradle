/**
 * Configure a Kotlin-JVM project with safe defaults.
 */

apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply from: "$rootDir/gradle/ktlint.gradle"

jacoco {
    toolVersion = versions_tools.Jacoco
}

dependencies {
    implementation meta_packages.CoreJVM

    testImplementation meta_packages.CoreTestJVM
}

// Define the Kotlin jvm target
compileKotlin {
    kotlinOptions.jvmTarget = versions_tools.JavaCompatibility
}

compileTestKotlin {
    kotlinOptions.jvmTarget = versions_tools.JavaCompatibility
}

// Enable outputting the results of the tests
test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Function to add a dependency to another JVM project.
ext.registerDependency = { project ->
    release.configure { dependsOn("${project}:releaseJvm") }
}

// Release task that will build and test the project.
tasks.register("releaseJvm") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
}

tasks.register("release") {
    dependsOn "releaseJvm"
}