/**
 * Plugin to create a JVM based CDK package.
 */
import org.apache.tools.ant.taskdefs.condition.Os

apply from: "$rootDir/gradle/kotlin-jvm-application.gradle"

dependencies {
    implementation "software.amazon.awscdk:aws-cdk-lib:_"
}

task synthesize(type:Exec) {
    group = 'cdk'
    description = 'Synthesize the cloud formation'

    workingDir "$projectDir"

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'cdk', 'synthesize'
    } else {
        commandLine "cdk", "synthesize"
    }
}

task forceDeploy(type:Exec) {
    group = 'cdk'
    description = 'Deploy this stack to the AWS account'

    workingDir "$projectDir"

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'cdk', 'deploy', "--all", '--require-approval', 'never'
    } else {
        commandLine "cdk", "deploy", "--all", "--require-approval", "never"
    }
}