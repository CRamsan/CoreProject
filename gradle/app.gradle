/*
Gradle plugin to create an Android app with good default values.
 */
apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt" // Kotlin Annotation Processor
apply plugin: "androidx.navigation.safeargs.kotlin" // Enable  safe-args for Jetpack Navigation
apply plugin: "dagger.hilt.android.plugin"
apply from: "$rootDir/gradle/jacoco-android.gradle"
apply from: "$rootDir/gradle/ktlint.gradle"

android {
    buildToolsVersion versions_tools.AndroidBuildToolsVersion
    compileSdkVersion versions_tools.CompileSdkVersion

    defaultConfig {
        minSdkVersion versions_tools.MinSdkVersion
        targetSdkVersion versions_tools.TargetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // Release build.
        release {
            // Minify is currently disabled. Maybe I should look into enabling this in the future?
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        // Debug settings enabled.
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    // Specifies one flavor dimension.
    flavorDimensions "stage"
    productFlavors {
        preprod {
            dimension "stage"
            applicationIdSuffix ".preprod"
            versionNameSuffix "-preprod"
        }
        prod {
            dimension "stage"
        }
    }

    sourceSets {
        // Add 'kotlin' as a valid source folder. The default directory is onyl 'src/main/java'.
        main {
            java.srcDirs += ['src/main/kotlin']
        }
        test {
            java.srcDirs += ['src/test/kotlin']
        }
        androidTest {
            java.srcDirs += ['src/androidTest/kotlin']
        }
    }

    buildFeatures {
        dataBinding true // Enable databinding https://developer.android.com/topic/libraries/data-binding
    }

    compileOptions {
        // Set the compile options for Java source
        sourceCompatibility versions_tools.JavaCompatibility
        targetCompatibility versions_tools.JavaCompatibility
    }

    // Why is this needed again?
    packagingOptions {
        exclude 'META-INF/**'
        exclude "META-INF/licenses/**"
        exclude "**/attach_hotspot_windows.dll" // https://github.com/Kotlin/kotlinx.coroutines/issues/2023
    }

    kotlinOptions {
        // Set the compile target for Kotlin source
        jvmTarget = versions_tools.JavaCompatibility
    }

    testOptions {
        unitTests {
            includeAndroidResources = true

            // Allows framework calls to return default values. I should look if I can remove this.
            // Instead we could relly on the testing framework to mock system calls.
            returnDefaultValues = true

            all {
                // Enable printing the result of the unit tests.
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    kapt meta_packages.HiltKapt

    implementation meta_packages.CoreAndroid
    implementation meta_packages.Hilt

    testImplementation meta_packages.CoreTestAndroid

    androidTestImplementation meta_packages.CoreAndrodTestAndroid
}

// Register a dependency to a Android library with the `releaseAndroid` task
ext.registerDependency = { project ->
    release.configure { dependsOn("${project}:releaseAndroid") }
}

// Register a dependency to a JVM library with the `releaseJVM` task
ext.registerJvmDependency = { project ->
    release.configure { dependsOn("${project}:releaseJvm") }
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn 'ktlint' // Verifies the format
    dependsOn 'build' // Assembles and test this project
    dependsOn 'jacocoAndroidTestReport' // Generate a jacoco report from the unit-test results
    dependsOn 'assembleAndroidTest' // Assemble the androidTest APK
}