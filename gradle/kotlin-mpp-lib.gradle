/**
 * Plugin to create a kotlin MPP with safe defaults.
 * This plugin requires that the project set the values for:
 * - libraryVersionCode
 * - libraryVersionName
 *
 * By default, the JVM, JS and IOS targets are created. You can
 * call the following functions to disable some of them:
 * disableJvmTarget()
 * disableIosTarget()
 * disableJsTarget()
 * disableAndroidTarget()
 */
apply plugin: "org.jetbrains.kotlin.multiplatform"
apply plugin: "org.jetbrains.dokka"

apply from: "$rootDir/gradle/helper-functions.gradle"
apply from: "$rootDir/gradle/detekt.gradle"

if (isJvmTargetEnabled()) {
    // Set the task name for the jacoco-mpp-jvm plugin
    ext {
        jvmTestTaskName = "jvmTest"
    }
    apply from: "$rootDir/gradle/jacoco-mpp-jvm.gradle"
}

if (isAndroidTargetEnabled()) {
    apply plugin: "com.android.library"
    apply plugin: "kotlin-kapt"

    apply from: "$rootDir/gradle/jacoco-android.gradle"

    android {
        buildToolsVersion versions_tools.AndroidBuildToolsVersion
        compileSdkVersion versions_tools.CompileSdkVersion
        defaultConfig {
            versionCode libraryVersionCode
            versionName libraryVersionName
            minSdkVersion versions_tools.MinSdkVersion
            targetSdkVersion versions_tools.TargetSdkVersion
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }
        compileOptions {
            sourceCompatibility versions_tools.JavaCompatibility
            targetCompatibility versions_tools.JavaCompatibility
        }

        testOptions {
            unitTests {
                includeAndroidResources = true
                all {
                    testLogging {
                        events "passed", "skipped", "failed"
                    }
                }
            }
        }
    }

    dependencies {
        implementation meta_packages.CoreAndroid

        testImplementation meta_packages.CoreTestAndroid

        androidTestImplementation meta_packages.CoreAndrodTestAndroid
    }
}

kotlin {
    targets.all {
        compilations.all {
            kotlinOptions {
                allWarningsAsErrors = true
                freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
            }
        }
    }

    if (isAndroidTargetEnabled()) {
        android("android") {
            compilations.all {
                kotlinOptions {
                    jvmTarget = versions_tools.JavaCompatibility
                }
            }
        }
    }

    if (isJvmTargetEnabled()) {
        jvm("jvm") {
            compilations.all {
                kotlinOptions {
                    jvmTarget = versions_tools.JavaCompatibility
                }
            }
        }
    }

    if (isIosTargetEnabled()) {
        // TODO: This target should be determined at configuration time since it depends on the
        // architecture of the host.
        iosSimulatorArm64("ios") {

        }
    }

    if (isJsTargetEnabled()) {
        js(IR) {
            binaries.executable()
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                // This syntax is used as the kotlin-mpp plugin does not support
                // arrays as arguments.
                meta_packages.CoreCommon.each { implementation it }
            }
        }
        commonTest {
            dependencies {
                // This syntax is used as the kotlin-mpp plugin does not support
                // arrays as arguments.
                // implementation packages.KotlinTestCommon
                meta_packages.CoreTestCommon.each { implementation it }
            }
        }
        if (isAndroidTargetEnabled()) {
            androidMain {
                dependsOn(commonMain)
            }
            androidTest {
                dependsOn(commonTest)
            }
        }
        if (isJvmTargetEnabled()) {
            jvmMain {
                dependsOn(commonMain)
                dependencies {
                    // This syntax is used as the kotlin-mpp plugin does not support
                    // arrays as arguments.
                    meta_packages.CoreJVM.each { implementation it }
                }
            }
            jvmTest {
                dependsOn(commonTest)
                dependencies {
                    // This syntax is used as the kotlin-mpp plugin does not support
                    // arrays as arguments.
                    meta_packages.CoreTestJVM.each { implementation it }

                    meta_packages.CoreTestJVMRuntimeOnly.each { runtimeOnly it }
                }
            }
        }
        if (isJsTargetEnabled()) {
            jsMain {
                dependsOn(commonMain)
                dependencies {
                    // This syntax is used as the kotlin-mpp plugin does not support
                    // arrays as arguments.
                    meta_packages.CoreJS.each { implementation it }
                }
            }
            jsTest {
                dependsOn(commonTest)
                dependencies {
                    // This syntax is used as the kotlin-mpp plugin does not support
                    // arrays as arguments.
                    meta_packages.CoreTestJS.each { implementation it }
                }
            }
        }
        if (isIosTargetEnabled()) {
            iosMain {
                dependsOn(commonMain)
            }
            iosTest {
                dependsOn(commonTest)
            }
        }
    }
}

tasks {
    if (isJvmTargetEnabled()) {
        jvmTest {
            useJUnitPlatform()
            testLogging {
                events "passed", "skipped", "failed"
            }
        }
    }
}

// TODO: Verify if this can be removed
// https://youtrack.jetbrains.com/issue/KT-46165
// https://youtrack.jetbrains.com/issue/KT-46978
tasks.withType(org.gradle.jvm.tasks.Jar) { duplicatesStrategy = DuplicatesStrategy.WARN }

def releaseJs = null
if (isJsTargetEnabled()) {
    releaseJs = tasks.register("releaseJs") {
        group = 'build'
        description = 'Run all the steps to build a Js artifact'
        dependsOn 'compileKotlinJs'
        dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
        dependsOn 'detektJsMain' // Run the code analyzer
        dependsOn 'jsTest'
    }
}

def releaseJvm = null
if (isJvmTargetEnabled()) {
    releaseJvm = tasks.register("releaseJvm") {
        group = 'build'
        description = 'Run all the steps to build a releaseJvm artifact'
        dependsOn 'compileKotlinJvm'
        dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
        dependsOn 'detektJvmMain' // Run the code analyzer
        dependsOn 'jvmTest'
        dependsOn 'jacocoJvmKMMTestReport'
    }
}

def releaseAndroid = null
if (isAndroidTargetEnabled()) {
    releaseAndroid = tasks.register("releaseAndroid") {
        group = 'build'
        description = 'Run all the steps to build a releaseAndroid artifact'
        dependsOn 'assembleDebug'
        dependsOn 'assembleRelease'
        dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
        dependsOn 'detektAndroidDebug' // Run the code analyzer
        dependsOn 'detektAndroidRelease' // Run the code analyzer
        dependsOn 'testDebugUnitTest'
        dependsOn 'testReleaseUnitTest'
        dependsOn 'jacocoAndroidTestReport' // Generate a jacoco report from the unit-test results
    }
}

def releaseIos = null
if (isIosTargetEnabled()) {
    releaseIos = tasks.register("releaseIos") {
        group = 'build'
        description = 'Run all the steps to build a releaseIos artifact'
        dependsOn 'compileKotlinIos'
        dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
        dependsOn 'detektIosMain' // Run the code analyzer
        dependsOn 'iosTest'
    }
}

def release = tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact for all enabled targets'

    if (isJvmTargetEnabled()) {
        dependsOn 'releaseJvm'
    }
    if (isAndroidTargetEnabled()) {
        dependsOn 'releaseAndroid'
    }
    if (isJsTargetEnabled()) {
        dependsOn 'releaseJs'
    }
    if (isIosTargetEnabled()) {
        dependsOn 'releaseIos'
    }
}

ext.registerDependency = { project ->
    if (isJvmTargetEnabled()) {
        def dependency = "${project}:releaseJvm"
        println("Adding dependency to $dependency")
        releaseJvm.configure { dependsOn(dependency) }
    }
    if (isAndroidTargetEnabled()) {
        def dependency = "${project}:releaseAndroid"
        println("Adding dependency to $dependency")
        releaseAndroid.configure { dependsOn(dependency) }
    }
    if (isJsTargetEnabled()) {
        def dependency = "${project}:releaseJs"
        println("Adding dependency to $dependency")
        releaseJs.configure { dependsOn(dependency) }
    }
}

// Register a dependency to a JVM library with the `releaseJVM` task
ext.registerJvmDependency = { project ->
    if (isJvmTargetEnabled()) {
        def dependency = "${project}:releaseJvm"
        println("Adding dependency to $dependency")
        releaseJvm.configure { dependsOn(dependency) }
    }
}
