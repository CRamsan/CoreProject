/**
 * Plugin to create a kotlin MPP with safe defaults.
 */
apply plugin: "org.jetbrains.kotlin.multiplatform"

apply plugin: "com.android.library"
apply plugin: "kotlin-kapt"
apply from: "$rootDir/gradle/detekt.gradle"

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    packaging {
        resources {
            excludes += "/META-INF/**"
        }
    }

    kotlin {
        jvmToolchain(17)
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            all {
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:_"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:_"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:_"

    testImplementation "androidx.test:core:_"
    testImplementation "androidx.test.ext:junit:_"
    testImplementation "androidx.test.ext:junit-ktx:_"
    testImplementation "androidx.arch.core:core-common:_"
    testImplementation "androidx.arch.core:core-runtime:_"
    testImplementation "androidx.arch.core:core-testing:_"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:_"
    testImplementation "junit:junit:_"
    testImplementation "org.jetbrains.kotlin:kotlin-test:_"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:_"
    testImplementation "io.mockk:mockk:_"
    testImplementation "io.mockk:mockk-android:_"

    androidTestImplementation "androidx.test.ext:junit:_"
    androidTestImplementation "androidx.test.ext:junit-ktx:_"
    androidTestImplementation "androidx.test:core:_"
    androidTestImplementation "androidx.test:rules:_"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:_"
    androidTestImplementation "junit:junit:_"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:_"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:_"
    androidTestImplementation "io.mockk:mockk-android:_"
    androidTestImplementation "org.robolectric:robolectric:_"
}

kotlin {
    targets.all {
        compilations.all {
        }
    }

    android() {
        compilations.all {
            kotlinOptions {

            }
        }
    }

    jvm() {
        compilations.all {
            kotlinOptions {
                jvmTarget = JavaVersion.VERSION_17
            }
        }
    }

    // TODO: This target should be determined at configuration time since it depends on the
    // architecture of the host.
    iosSimulatorArm64("ios") {

    }

    js {
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:_"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:_"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:_"
                implementation "org.jetbrains.kotlin:kotlin-test-common:_"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:_"
            }
        }
        androidMain {
            dependsOn(commonMain)
        }
        androidUnitTest {
            dependsOn(commonTest)
        }
        jvmMain {
            dependsOn(commonMain)
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:_"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:_"
            }
        }
        jvmTest {
            dependsOn(commonTest)
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:_"
                implementation "org.junit.jupiter:junit-jupiter-api:_"
                implementation "org.jetbrains.kotlin:kotlin-test-junit5:_"
                implementation "io.mockk:mockk:_"

                runtimeOnly Testing.junit.jupiter.engine
            }
        }
        jsMain {
            dependsOn(commonMain)
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:_"
            }
        }
        jsTest {
            dependsOn(commonTest)
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js:_"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:_"
            }
        }
        iosMain {
            dependsOn(commonMain)
        }
        iosTest {
            dependsOn(commonTest)
        }
    }
}

tasks {
    jvmTest {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

tasks.register("releaseJs") {
    group = 'release'
    description = 'Run all the steps to build a Js artifact'
    dependsOn 'compileKotlinJs'
    dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
    dependsOn 'detektJsMain' // Run the code analyzer
    dependsOn 'jsTest'
}

tasks.register("releaseJvm") {
    group = 'release'
    description = 'Run all the steps to build a releaseJvm artifact'
    dependsOn 'compileKotlinJvm'
    dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
    dependsOn 'detektJvmMain' // Run the code analyzer
    dependsOn 'jvmTest'
}

tasks.register("releaseAndroid") {
    group = 'release'
    description = 'Run all the steps to build a releaseAndroid artifact'
    dependsOn 'assembleDebug'
    dependsOn 'assembleRelease'
    dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
    dependsOn 'detektAndroidDebug' // Run the code analyzer
    dependsOn 'detektAndroidRelease' // Run the code analyzer
    dependsOn 'testDebugUnitTest'
    dependsOn 'testReleaseUnitTest'
}

tasks.register("releaseIos") {
    group = 'release'
    description = 'Run all the steps to build a releaseIos artifact'
    dependsOn 'compileKotlinIos'
    dependsOn 'detektMetadataMain' // Run the code analyzer on the common-code source set
    dependsOn 'detektIosMain' // Run the code analyzer
    dependsOn 'iosTest'
}

tasks.register("release") {
    group = 'release'
    description = 'Run all the steps to build a release artifact for all enabled targets'

    dependsOn 'releaseJvm'
    dependsOn 'releaseAndroid'
    dependsOn 'releaseJs'
    dependsOn 'releaseIos'
}