/*
Gradle plugin to create an Android app with good default values.
 */
apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt" // Kotlin Annotation Processor
apply plugin: "androidx.navigation.safeargs.kotlin" // Enable  safe-args for Jetpack Navigation
apply plugin: "dagger.hilt.android.plugin"
apply plugin: "org.jetbrains.dokka"
apply from: "$rootDir/gradle/jacoco-android.gradle"
apply from: "$rootDir/gradle/detekt.gradle"

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // Debug settings enabled.
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            kotlinOptions {
                allWarningsAsErrors = true
            }
        }
        // Release build.
        release {
            // Minify is currently disabled. Maybe I should look into enabling this in the future?
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            kotlinOptions {
                allWarningsAsErrors = true
            }
        }
    }
    // Specifies one flavor dimension.
    flavorDimensions "stage"
    productFlavors {
        preprod {
            dimension "stage"
            applicationIdSuffix ".preprod"
            versionNameSuffix "-preprod"
        }
        prod {
            dimension "stage"
        }
    }

    buildFeatures {
    }

    compileOptions {
        // Set the compile options for Java source
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Why is this needed again?
    packagingOptions {
        exclude 'META-INF/**'
        exclude "META-INF/licenses/**"
        exclude "**/attach_hotspot_windows.dll" // https://github.com/Kotlin/kotlinx.coroutines/issues/2023
    }

    kotlin {
        jvmToolchain(17)
    }

    kotlinOptions {
        // Set the compile target for Kotlin source
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true

            // Allows framework calls to return default values. I should look if I can remove this.
            // Instead we could relly on the testing framework to mock system calls.
            returnDefaultValues = true

            all {
                // Enable printing the result of the unit tests.
                testLogging {
                    events "passed", "skipped", "failed"
                }
            }
        }
    }
}

dependencies {
    kapt Google.dagger.hilt.android.compiler
    kapt Google.dagger.hilt.android.gradlePlugin

    implementation Kotlin.stdlib.jdk8
    implementation KotlinX.coroutines.core
    implementation KotlinX.coroutines.android
    implementation Google.dagger.hilt.android

    testImplementation AndroidX.test.core
    testImplementation AndroidX.test.ext.junit
    testImplementation AndroidX.test.ext.junit.ktx
    testImplementation AndroidX.archCore.common
    testImplementation AndroidX.archCore.runtime
    testImplementation AndroidX.archCore.testing
    testImplementation KotlinX.coroutines.test
    testImplementation Testing.junit4
    testImplementation Kotlin.test
    testImplementation Kotlin.test.junit
    testImplementation Testing.mockK
    testImplementation Testing.mockK.android

    androidTestImplementation AndroidX.test.ext.junit
    androidTestImplementation AndroidX.test.ext.junit.ktx
    androidTestImplementation AndroidX.test.core
    androidTestImplementation AndroidX.test.rules
    androidTestImplementation KotlinX.coroutines.test
    androidTestImplementation Testing.junit4
    androidTestImplementation Kotlin.test
    androidTestImplementation Kotlin.test.junit
    androidTestImplementation Testing.mockK.android
    androidTestImplementation Testing.robolectric
}

// Register a dependency to a Android library with the `releaseAndroid` task
ext.registerDependency = { project ->
    def dependency = "${project}:releaseAndroid"
    release.configure { dependsOn(dependency) }
}

// Register a dependency to a JVM library with the `releaseJVM` task
ext.registerJvmDependency = { project ->
    def dependency = "${project}:releaseJvm"
    release.configure { dependsOn(dependency) }
}

tasks.register("releaseAndroid") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn 'build' // Assembles and test this project
    dependsOn 'detekt' // Run the code analyzer
    dependsOn 'jacocoAndroidTestReport' // Generate a jacoco report from the unit-test results
    dependsOn 'assembleAndroidTest' // Assemble the androidTest APK
}

tasks.register("release") {
    group = 'build'
    description = 'Run releaseAndroid'

    dependsOn 'releaseAndroid'
}
