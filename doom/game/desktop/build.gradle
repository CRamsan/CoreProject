plugins {
    id 'java'
}

sourceCompatibility = globalJavaCompatibility
sourceSets.main.java.srcDirs = [ "src/" ]

project.ext.mainClassName = "com.cramsan.awsgame.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../android/assets")

dependencies {
    implementation project(":doom:game:core")
    implementation packages.GdxLwjgl3
    implementation packages.GdxDesktop
}

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task debug(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    from files(sourceSets.main.output.getClassesDirs())
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir)
 
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

dist.dependsOn classes

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn ':doom:game:core:release'
    dependsOn 'build'
    dependsOn 'test'
}