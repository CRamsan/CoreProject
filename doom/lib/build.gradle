plugins {
    id "org.jetbrains.kotlin.multiplatform"
}

/**
 * Configure multiplatform project
 */
kotlin {
    jvm("jvm") {
        compilations.all {
            kotlinOptions {
                jvmTarget = versions_tools.JavaCompatibility
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation project(":framework")
                implementation packages.Kodein
                implementation packages.KotlinStdCommon
                implementation packages.CoroutinesCore
            }
        }
        commonTest {
            dependencies {
                implementation packages.Mockk
                implementation packages.KotlinTestCommon
                implementation packages.KotlinTestAnnotations
            }
        }
        jvmMain {
            dependencies {
                implementation packages.KotlinStdJdk8
                implementation packages.CoroutinesCore
            }
        }
        jvmTest {
            dependencies {
                implementation packages.KotlinTest
                implementation packages.KotlinTestJunit
                implementation packages.CoroutinesSwing
            }
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                allWarningsAsErrors = true
            }
        }
    }
}

/**
 * Log test results
 */
tasks {
    jvmTest {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

configurations {
    // https://youtrack.jetbrains.com/issue/KT-27170
    compileClasspath
}

tasks.register("release") {
    group = 'build'
    description = 'Run all the steps to build a release artifact'

    dependsOn ':framework:release'
    dependsOn 'ktlint'
    dependsOn 'build'
    dependsOn 'allTests'
}