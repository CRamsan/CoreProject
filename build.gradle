buildscript {
    ext {
        globalCompileSdkVersion = 28
        globalMinSdkVersion = 24
        globalTargetSdkVersion = 28
        globalVersionCode = 1
        globalVersionName = "1.0"

        globalJavaCompatibility = JavaVersion.VERSION_1_8

        globalAndroidBuildToolsVersion = "3.4.1"
        globalAndroidXJUnitVersion = "1.1.1"
        globalCoroutinesVersion = "1.3.0-M2"
        globalEspressoVersion = "3.2.0"
        globalGlideVersion = "4.9.0"
        globalJUnitVersion = "4.12"
        globalKodeinVersion = "6.3.3"
        globalKotlinVersion = "1.3.40"
        globalMockkVersion = "1.9.3"
        globalRobolectricVersion = "4.3"
        globalSqlDelightVersion = "1.1.4"
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidBuildToolsVersion"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.21.0"
    id "org.jetbrains.kotlin.multiplatform" version "1.3.40" apply false
    id "org.jetbrains.kotlin.android" version "1.3.40" apply false
    id "org.jetbrains.kotlin.android.extensions" version "1.3.40" apply false
    id "org.jetbrains.kotlin.kapt" version "1.3.40" apply false
}

subprojects {

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:0.34.2"
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "src/**/*.kt"
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "src/**/*.kt"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
