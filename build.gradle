buildscript {
    ext {
        globalAndroidBuildToolsVersion = "29.0.2"
        globalCompileSdkVersion = 29
        globalMinSdkVersion = 22
        globalTargetSdkVersion = 29
        globalVersionCode = 1
        globalVersionName = "1.0"

        globalJavaCompatibility = JavaVersion.VERSION_1_8

        globalAndroidGradlePluginVersion = "3.5.2"
        globalAndroidMaterialVersion = "1.0.0"
        globalAndroidXJUnitVersion = "1.1.1"
        globalAndroidXLifecycleVersion = "2.1.0"
        globalAndroidXRecyclerViewVersion = "1.0.0"
        globalAppCenterSdkVersion = "2.4.1"
        globalCoroutinesVersion = "1.2.2"
        globalConstraintLayoutVersion = "1.1.3"
        globalDetektVersion = "1.0.0"
        globalEspressoVersion = "3.2.0"
        globalFirebaseVersion = "18.2.0"
        globalGlideVersion = "4.10.0"
        globalGsonVerion = "2.8.5"
        globalJUnitVersion = "4.12"
        globalKodeinVersion = "6.4.1"
        globalKotlinVersion = "1.3.60"
        globalKtlintVerion = "0.35.0"
        globalKtorVersion = "1.2.3"
        globalMockkVersion = "1.9.3"
        globalOkhttpVersion = "4.1.0"
        globalRobolectricVersion = "4.3.1"
        globalSqlDelightVersion = "1.2.0"

        globalGdxVersion = '1.9.9'
        globalRoboVMVersion = '2.3.6'
        globalBox2DLightsVersion = '1.4'
        globalAshleyVersion = '1.7.0'
        globalAiVersion = '1.8.0'
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidGradlePluginVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt"
}

subprojects {

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:${globalKtlintVerion}"
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    task("ktlint", type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/checkstyle-result.xml", "${project.projectDir}/src/**/*.kt"
    }

    task("ktlintFormat", type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "${project.projectDir}/src/**/*.kt"
    }

    detekt {
        toolVersion = "${globalDetektVersion}"
        input = files("${project.projectDir}/src/")

        filters = "${project.projectDir}/.*_/resources/.*,${project.projectDir}/.*_/build/.*"
        config = files("detekt-config.yml", "${project.projectDir}/detekt-config.yml")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
