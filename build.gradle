buildscript {
    ext {
        globalCompileSdkVersion = 28
        globalMinSdkVersion = 22
        globalTargetSdkVersion = 28
        globalVersionCode = 1
        globalVersionName = "1.0"

        globalJavaCompatibility = JavaVersion.VERSION_1_8

        globalAndroidBuildToolsVersion = "3.5.0"
        globalAndroidMaterialVersion = "1.0.0"
        globalAndroidXJUnitVersion = "1.1.1"
        globalAndroidXLifecycleVersion = "2.1.0"
        globalAndroidXRecyclerViewVersion = "1.0.0"
        globalAppCenterSdkVersion = "2.3.0"
        globalCoroutinesVersion = "1.2.2"
        globalConstraintLayoutVersion = "1.1.3"
        globalDetektVersion = "1.0.0"
        globalEspressoVersion = "3.2.0"
        globalFirebaseVersion = "18.1.1"
        globalGlideVersion = "4.9.0"
        globalGsonVerion = "2.8.5"
        globalJUnitVersion = "4.12"
        globalKodeinVersion = "6.3.3"
        globalKotlinVersion = "1.3.40"
        globalKtlintVerion = "0.35.0"
        globalKtorVersion = "1.2.3"
        globalMockkVersion = "1.9.3"
        globalOkhttpVersion = "4.1.0"
        globalRobolectricVersion = "4.3"
        globalSqlDelightVersion = "1.2.0"
    }

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$globalAndroidBuildToolsVersion"
    }
}

plugins {
    id "com.github.ben-manes.versions"
    id "io.gitlab.arturbosch.detekt"
    id "se.patrikerdes.use-latest-versions"
}

subprojects {

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:${globalKtlintVerion}"
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    task("ktlint", type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/checkstyle-result.xml", "${project.projectDir}/src/**/*.kt"
    }

    task("ktlintFormat", type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        classpath = configurations.ktlint
        main = "com.pinterest.ktlint.Main"
        args "-F", "${project.projectDir}/src/**/*.kt"
    }

    detekt {
        toolVersion = "${globalDetektVersion}"
        input = files("${project.projectDir}/src/")

        filters = "${project.projectDir}/.*_/resources/.*,${project.projectDir}/.*_/build/.*"
        config = files("detekt-config.yml", "${project.projectDir}/detekt-config.yml")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
